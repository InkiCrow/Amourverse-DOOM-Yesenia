ACTOR HasShotRight : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR GhoulShot
{
	scale 0.8
	height 0
	radius 3
	damage 8
	speed 90
	deathsound "BSTR_HIT"
	Decal "GHLPLAS"
	PROJECTILE
	+FORCEPAIN
	
	States
	{
		Spawn:
		TNT1 A 0 A_AttachLight("bustershot", 1, "FA5858", 20, 35, 0)
		GLPJ A 2 Bright
		Loop
		
		Death:
		TNT1 A 0 A_AttachLight("bustershot", 1, "FA5858", 40, 70, 0)
		GLPF ABCD 2 Bright
		TNT1 A 0 A_RemoveLight("bustershot")
		Stop
	}
}

ACTOR GhoulTrail
{
	Scale 0.5
	RenderStyle Add
	+NOGRAVITY
	+NOBLOCKMAP
	States
	{
		Spawn:
		TNT1 A 0 A_AttachLight("ghoultrail", 1, "FA5858", 20, 50, 0)
		GLSP D 1 BRIGHT
		Death:
		GLSP EF 1 BRIGHT
		TNT1 A 0 A_RemoveLight("ghoultrail")
		Stop
	}
}

ACTOR SuperGhoulShot
{
	scale 1
	height 0
	radius 8
	damage 50
	speed 60
	deathsound "BSTR_HIT"
	Decal "GHLPLAL"
	PROJECTILE
	+FORCEPAIN
	
	States
	{
		Spawn:
		TNT1 A 0 A_AttachLight("sbustershot", 1, "FA5858", 20, 35, 0)
		GLSP ABC 2 Bright A_SpawnItemEX("GhoulTrail")
		Loop
		
		Death:
		TNT1 A 0 A_AttachLight("sbustershot", 1, "FA5858", 40, 70, 0)
		GLSP DEF 2 Bright
		TNT1 A 0 A_RemoveLight("sbustershot")
		Stop
	}
}

ACTOR TwinGhoulies : YeseniaArms
{
	Weapon.SelectionOrder 2
	Weapon.AmmoType "GhostClip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 15
	Weapon.BobSpeed 2.0
	Weapon.BobRangeX 0.4
	Weapon.BobRangeY 0.2
	Inventory.Icon "YTGHA0"
	+ALT_USES_BOTH
	+NOAUTOFIRE
	+NOAUTOAIM
	
	States
	{
		Spawn:
		YTGH A 1
		Loop
		
		Select:
		TNT1 A 0 A_JumpIf(GetCVar("y_enablelegs") == 0, 2)
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 0 A_Raise(255)
		Wait
		
		DrawGun:
		TNT1 A 0 A_PlaySound("buster/spin", CHAN_BODY, 0.6)
		GHOU ABCD 2
		TNT1 A 0 A_GiveInventory("GhouliesEquipped", 1)
		Goto Ready
		
		HolsterGun:
		GHOU DCBA 2
		TNT1 A 0 A_TakeInventory("GhouliesEquipped", 1)
		Goto Deselect
		
		Deselect:
		TNT1 A 0 A_JumpIfInventory("GhouliesEquipped", 1, "HolsterGun")
		TNT1 A 0 A_RemoveLight("ghoulflash")
		TNT1 A 0 A_Lower(255)
		Wait
		
		Reselect:
		GHOU ABCD 2
		Goto Ready
		
		Ready:
		TNT1 A 0 A_JumpIfInventory("IsClimbing", 1, "LedgeGrab")
		TNT1 A 0 A_JumpIfInventory("GhouliesEquipped", 0, 1)
		Goto DrawGun
		GHOU E 1 A_WeaponReady(WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWUSER3)
		Loop
		
		Fire:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		TNT1 A 0 A_JumpIfInventory("HasShotRight", 1, "FireLeft")
		TNT1 A 0 A_Jump(255, "FireRight")
		
		FireRight:
		TNT1 A 0 A_AttachLight("ghoulflash", 1, "FA5858", 40, 65, 0)
		GHOU F 1 BRIGHT
		GHOU G 1 BRIGHT A_PlaySound ("buster/shot", CHAN_WEAPON, 0.7)
		TNT1 A 0 A_FireProjectile("GhoulShot", 0, TRUE, 0, 5)
		TNT1 A 0 A_RemoveLight("ghoulflash")
		GHOU HI 1
		GHOU JK 2
		TNT1 A 0 A_GiveInventory("HasShotRight", 1)
		Goto Ready
		
		FireLeft:
		TNT1 A 0 A_AttachLight("ghoulflash", 1, "FA5858", 40, 65, 0)
		GHOU L 1 BRIGHT
		GHOU M 1 BRIGHT A_PlaySound ("buster/shot", CHAN_WEAPON, 0.7)
		TNT1 A 0 A_FireProjectile("GhoulShot", 0, TRUE, 0, 5)
		TNT1 A 0 A_RemoveLight("ghoulflash")
		GHOU NO 1
		GHOU PQ 2
		TNT1 A 0 A_TakeInventory("HasShotRight", 1)
		Goto Ready
		
		AltFire:
		TNT1 A 0 A_JumpIfInventory("GhostClip", 5, "ConfirmAltFire")
		GHOU M 0 A_PlaySound ("yesenia/usefail", CHAN_VOICE, 1)
		Goto Ready
		
		ConfirmAltFire:
		GHOU R 2 BRIGHT A_PlaySound("buster/bigshot", CHAN_WEAPON, 1)
		GHOU ST 2 BRIGHT
		GHOU U 2 A_FireProjectile("SuperGhoulShot", 0, FALSE, 0, 5)
		TNT1 A 0 A_JumpIfInventory("PowerInfiniteAmmo", 1, 2)
		TNT1 A 0 A_TakeInventory("GhostClip", 5)
		GHOU VW 2
		GHOU XX 2
		GHOU YZ 2
		GHOZ AB 2
		Goto Ready
		
		User1:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Kick")
		Goto Ready
		
		User2:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Slide")
		Goto Ready
		
		User3:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "GroundSlam")
		Goto Ready
	}
}