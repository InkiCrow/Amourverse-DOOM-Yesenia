ACTOR ScarfFists : YeseniaArms Replaces Fist 20000
{
	Weapon.SelectionOrder 1
	AttackSound "yesenia/kickhit"
	Weapon.BobSpeed 2.0
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.15
	
	States
	{
		Select:
		TNT1 A 0 A_JumpIf(GetCVar("y_enablelegs") == 0, 2)
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 0 A_Raise(255)
		//YPCH D A_Raise
		Wait
		
		DrawGun:
		YPCH ABCD 2
		TNT1 A 0 A_GiveInventory("ScarfEquipped", 1)
		Goto Ready
		
		HolsterGun:
		YPCH DBA 2
		TNT1 A 0 A_TakeInventory("ScarfEquipped", 1)
		Goto Deselect
		
		Deselect:
		TNT1 A 0 A_JumpIfInventory("ScarfEquipped", 1, "HolsterGun")
		TNT1 A 0 A_Lower(255)
		Wait
		
		Reselect:
		YPCH ABCD 2
		Goto Ready
		
		Ready:
		TNT1 A 0 A_JumpIfInventory("IsClimbing", 1, "LedgeGrab")
		TNT1 A 0 A_JumpIfInventory("ScarfEquipped", 0, 1)
		Goto DrawGun
		YPCH D 1 A_WeaponReady(WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWUSER3)
		Loop
		
		Fire:
		YPCH EFG 2
		Goto Hold
		
		AltFire:
		YPCH BA 2
		YPAT ABCD 2
		Goto AltHold
		
		User1:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Kick")
		Goto Ready
		
		User2:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Slide")
		Goto Ready
		
		User3:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "GroundSlam")
		Goto Ready
		
		Hold:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
		NormalPunch:
		YPCH HIJ 1
		YPCH K 0 A_PlaySound ("yesenia/scarfpnch", CHAN_WEAPON, 0.5)
		TNT1 A 0 A_PlaySound ("yesenia/punch", CHAN_VOICE, 1)
		YPCH K 2 A_CustomPunch(8, FALSE, 0, "ScarfPunchEffect", 70, 0, 0, "none", "KHIT_SND")
		YPCH LMHNOP 1
		YPCH Q 0 A_PlaySound ("yesenia/scarfpnch", CHAN_WEAPON, 0.5)
		TNT1 A 0 A_PlaySound ("yesenia/punch", CHAN_VOICE, 1)
		YPCH Q 2 A_CustomPunch(8, FALSE, 0, "ScarfPunchEffect", 70, 0, 0, "none", "KHIT_SND")
		YPCH RST 1
		TNT1 A 0 A_ReFire
		Goto ReadyAgain
		Berserked:
		YPFL A 1 {
			A_PlaySound ("yesenia/barrage", CHAN_WEAPON, 0.5);
			A_CustomPunch(15, FALSE, 0, "ScarfPunchEffect", 70, 0, 0, "none", "KHIT_SND");
		}
		YPFL BCDEF 1 A_CustomPunch(15, FALSE, 0, "ScarfPunchEffect", 70, 0, 0, "none", "KHIT_SND")
		YPFL A 1 {
			A_PlaySound ("yesenia/punch", CHAN_VOICE, 1);
			A_CustomPunch(15, FALSE, 0, "ScarfPunchEffect", 70, 0, 0, "none", "KHIT_SND");
		}
		YPFL BCDEF 1 A_CustomPunch(15, FALSE, 0, "ScarfPunchEffect", 70, 0, 0, "none", "KHIT_SND")
		TNT1 A 0 A_ReFire
		Goto ReadyAgain
		
		AltHold:
		YPAT EF 2
		TNT1 A 0 A_ReFire
		YPAT FDCBAGH 2
		TNT1 A 0 A_Recoil(-25)
		TNT1 A 0 A_PlaySound ("yesenia/scarfpnch", CHAN_WEAPON, 1)
		TNT1 A 0 A_PlaySound ("yesenia/hevshout", CHAN_VOICE, 1)
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 4)
		YPAT IJ 2 A_CustomPunch(30, FALSE, 0, "ScarfPunchEffect", 80, 0, 0, "none", "KHIT_SND")
		TNT1 A 0 A_Jump(255, 3)
		YPAT IJ 2 A_CustomPunch(150, FALSE, 0, "ScarfPunchEffect", 80, 0, 0, "none", "KHIT_SND")
		YPAT KL 2
		YPCH ABC 2
		Goto Ready
		
		ReadyAgain:
		YPCH HUVWABC 2
		Goto Ready
		
		
	}
}