ACTOR SlamFallTime : Inventory
{
	-INVBAR
	Inventory.MaxAmount 3
}

ACTOR SlamWave
{
	height 16
	radius 8
	damage 50
	speed 30
	vspeed 2
	RenderStyle Translucent
	Alpha 0.7
	Projectile
	
	States
	{
		Spawn:
		GSWV A 3
		Death:
		GSWV BCD 3
		Stop
	}
}

ACTOR YeseniaArms : Weapon
{
	Weapon.Kickback 200
	Inventory.PickupSound "pickup/gun"
	+NOAUTOFIRE
	
	States {
	//This section is for the kick move
	
	Kick:
	TNT1 A 0 A_JumpIf(GetCVar("y_enablekick") == 0, "Ready")
	TNT1 A 0 A_JumpIfInventory("IsSliding", 1, "SlideKick")
	TNT1 A 0 A_GiveInventory("IsKicking", 1)
	YKCK A 0 A_PlaySound("yesenia/kick", CHAN_WEAPON, 0.5)
	TNT1 A 0 A_PlaySound ("yesenia/shout", CHAN_VOICE, 1)
	YKCK ABCDE 1
	TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 3)
	YKCK F 1 A_CustomPunch(10, FALSE, 0, "KickEffect", 70, 0, 0, "none", "KHIT_SND")
	TNT1 A 0 A_Jump(256, 2)
	YKCK F 1 A_CustomPunch(30, FALSE, 0, "KickEffect", 70, 0, 0, "none", "KHIT_SND")
	YKCK F 2 
	YKCK GH 1
	EMPT A 0 A_TakeInventory("IsKicking", 1)
	EMPT A 0 A_Jump(256, "Reselect")
	Stop
	
	//Here's the ledgegrab
	LedgeGrab:
	TNT1 A 0 A_TakeInventory("IsClimbing", 1)
	TNT1 A 0 A_PlaySound("yesenia/climb", CHAN_AUTO)
	TNT1 A 0 A_PlaySound ("yesenia/land", CHAN_VOICE, 1)
	YGRB AB 2 A_SetPitch(pitch-2, SPF_INTERPOLATE)
	YGRB CD 2
	YGRB EF 2 A_SetPitch(pitch+2, SPF_INTERPOLATE)
	EMPT A 0 A_Jump(256, "Reselect")
	Wait
	//Here's the slide
	Slide:
	TNT1 A 0 A_JumpIf(GetCVar("y_enableslide") == 0, "Ready")
	TNT1 A 0 A_PlaySound("yesenia/slide", CHAN_AUTO)
	TNT1 A 0
	{
		A_SetSize(-1, 24);
		ACS_NamedExecute("SetSlideView", 0);
	}
	TNT1 A 0 A_SetPitch(0, SPF_INTERPOLATE)
	TNT1 A 0 A_SetInventory("IsSliding", 1)
	TNT1 A 0 A_ZoomFactor(0.750)
	YSLD AB 2 A_Recoil(-10)
	YSLD C 4
	{
		A_CustomPunch(2, FALSE, 0, "none", 70, 0, 0, "none", "KHIT_SND");
		A_WeaponReady(WRF_ALLOWUSER1|WRF_NOFIRE);
	}
	TNT1 A 0 A_ZoomFactor(0.875)
	YSLD DE 2
	{
		A_Recoil(-5);
		A_WeaponReady(WRF_ALLOWUSER1|WRF_NOFIRE);
	}
	TNT1 A 0 A_ZoomFactor(1)
	TNT1 A 0 A_SetInventory("IsSliding", 0)
	TNT1 A 0
	{
		A_SetSize(-1, 56);
		ACS_NamedExecute("UnSetSlideView", 0);
	}
	EMPT A 0 A_Jump(256, "Reselect")
	Stop
	//This fucntion is for the slide kick move
	SlideKick:
	TNT1 A 0 A_GiveInventory("IsKicking", 1)
	TNT1 A 0 A_ZoomFactor(1)
	TNT1 A 0 A_SetInventory("IsSliding", 0)
	TNT1 A 0
	{
		A_SetSize(-1, 56);
		ACS_NamedExecute("UnSetSlideView", 0);
	}
	YSLD E 2
	TNT1 A 0 A_PlaySound("yesenia/kick", CHAN_WEAPON, 0.5)
	TNT1 A 0 A_PlaySound ("yesenia/shout", CHAN_VOICE, 1)
	YSLK AB 1 A_Recoil(-20)
	TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 5)
	YSLK CDE 1 A_CustomPunch(15, FALSE, 0, "KickEffect", 70, 0, 0, "none", "KHIT_SND")
	TNT1 A 0 A_Jump(256, 4)
	YSLK CDE 1 A_CustomPunch(40, FALSE, 0, "KickEffect", 70, 0, 0, "none", "KHIT_SND")
	YSLK F 2 
	YSLK G 2 A_TakeInventory("IsKicking", 1)
	EMPT A 0 A_Jump(256, "Reselect")
	
	//This is where the groundpount move will be found.
	GroundSlam:
	TNT1 A 0 A_JumpIf(GetCVar("y_enablegroundslam") == 0, "Ready")
	GSLM A 2 A_PlaySound("yesenia/slamdown", CHAN_BODY, 1)
	GSLM B 2 A_ChangeVelocity(0, 0, -30)
	GroundSlamLoop:
	TNT1 A 0 A_JumpIf(velz == 0, "GroundSlamEnd")
	GSLM CD 2
	TNT1 A 0 A_GiveInventory("SlamFallTime", 1)
	Loop
	GroundSlamEnd:
	TNT1 A 0 A_JumpIfInventory("SlamFallTime", 1, "GroundSlamHardEnd")
	GSLM E 2
	GSLM F 6
	GSLM GH 2
	{
		A_PlaySound("yesenia/slamland", CHAN_BODY, 1);
		A_Explode(50, 150, 0);
		A_SetInventory("SlamFallTime", 0);
	}
	EMPT A 0 A_Jump(256, "Reselect")
	GroundSlamHardEnd:
	GSLM E 2
	GSLM F 6
	GSLM GH 2
	{
		A_PlaySound("yesenia/slamhard", CHAN_BODY, 1);
		A_Explode(120, 250, 0);
		A_FireProjectile("SlamWave", 0, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 45, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 90, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 135, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 180, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 225, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 270, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_FireProjectile("SlamWave", 315, FALSE, 0, 8, FPF_NOAUTOAIM, 0);
		A_SetInventory("SlamFallTime", 0);
	}
	EMPT A 0 A_Jump(256, "Reselect")
	
	//Here will be where the feet will be visible. Credit to PA1NKI113R (Project Brutality) for the original script.
	FirstPersonLegsHide:
		TNT1 A 1
		TNT1 A 0 A_JumpIfInventory("IsSliding", 0, 2)
		Loop
	FirstPersonLegsStand:
		TNT1 A 0 
		TNT1 A 0 A_JumpIfInventory("IsKicking", 1, "FirstPersonLegsKick")
		TNT1 A 0 A_JumpIf(velx > 0.8 || velx < -0.8 || vely > 0.8 || vely < -0.8, "FirstPersonLegsWalk1")
		TNT1 A 0 A_JumpIf(velZ != 0, "FirstPersonLegsJump")
		TNT1 A 0 A_JumpIf(Height < 54, "FirstPersonLegsCrouch")
		TNT1 A 0 A_JumpIfInventory("IsSliding", 1, 2)
		//TNT1 A 0 A_JumpIfInventory("FPSLegChecker", 1, "FirstPersonLegsHide")
		YLEG A 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
		}
		Loop
		TNT1 A 0 
		TNT1 A 0 A_JumpIf(Height < 54, "FirstPersonLegsCrouch")
		TNT1 A 0 A_JumpIf(velZ != 0, "FirstPersonLegsJump")
		YLEG A 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
		}
		Loop
		
	FirstPersonLegsCrouch:
		TNT1 A 0 
		//TNT1 A 0 A_PlaySound("IronSights", 0) //Sound when player crouches
		FirstPersonLegsCrouchContinue:
		TNT1 A 0 A_JumpIf(momZ != 0, "FirstPersonLegsJump")
		TNT1 A 0 A_JumpIf(velx > 0.8 || velx < -0.8 || vely > 0.8 || vely < -0.8, "FirstPersonLegsWalk1")
		TNT1 A 0 A_JumpIf(Height > 54, "FirstPersonLegsStand")
		YLEG Q 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
		}
		Loop
		
	FirstPersonLegsWalk1:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("FPSLegChecker", 1, "FirstPersonLegsWalk2")
		TNT1 A 0 A_JumpIf(velZ != 0, "FirstPersonLegsJump")
		TNT1 A 0 A_JumpIf(velx > 8 || velx < -8 || vely > 8 || vely < -8, "FirstPersonLegsRun1")
		TNT1 A 0 A_GiveInventory("FPSLegChecker",1)
		TNT1 A 0 A_JumpIf(GetCVar("y_enablefootstepsounds") == 0, 2)
		TNT1 A 0 A_PlaySound("yesenia/footstep", CHAN_5, 0.4)
		YLEG EEFFGGHHIIJJKK 1
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
			A_JumpIf(velZ != 0, "FirstPersonLegsJump");
		}
		Goto FirstPersonLegsStand
		
	FirstPersonLegsWalk2:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(velZ != 0, "FirstPersonLegsJump")
		TNT1 A 0 A_TakeInventory("FPSLegChecker",1)
		TNT1 A 0 A_JumpIf(velx > 8 || velx < -8 || vely > 8 || vely < -8, "FirstPersonLegsRun2")
		TNT1 A 0 A_JumpIf(GetCVar("y_enablefootstepsounds") == 0, 2)
		TNT1 A 0 A_PlaySound("yesenia/footstep", CHAN_5, 0.4)
		YLEG LLMMNNOOBBCCDD 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
			A_JumpIf(velZ != 0, "FirstPersonLegsJump");
		}
		Goto FirstPersonLegsStand
	
	FirstPersonLegsRun1:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("FPSLegChecker", 1, "FirstPersonLegsWalk2")
		TNT1 A 0 A_JumpIf(velZ != 0, "FirstPersonLegsJump")
		TNT1 A 0 A_GiveInventory("FPSLegChecker",1)
		TNT1 A 0 A_JumpIf(GetCVar("y_enablefootstepsounds") == 0, 2)
		TNT1 A 0 A_PlaySound("yesenia/footstep", CHAN_5, 0.4)
		YLEG EFGHIJK 1
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
			A_JumpIf(velZ != 0, "FirstPersonLegsJump");
		}
		Goto FirstPersonLegsStand
		
	FirstPersonLegsRun2:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(velZ != 0, "FirstPersonLegsJump")
		TNT1 A 0 A_TakeInventory("FPSLegChecker",1)
		TNT1 A 0 A_JumpIf(GetCVar("y_enablefootstepsounds") == 0, 2)
		TNT1 A 0 A_PlaySound("yesenia/footstep", CHAN_5, 0.4)
		YLEG LMNOBCD 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
			A_JumpIf(velZ != 0, "FirstPersonLegsJump");
		}
		Goto FirstPersonLegsStand	
		
		
	FirstPersonLegsJump:
		TNT1 A 0 
		YLEG P 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
		}
		Goto FirstPersonLegsStand
	
	FirstPersonLegsKick:
		TNT1 A 0 
		YLEG R 1 
		{
			A_OverlayOffset(-10, 0, (-pitch*2)+200);
		}
		Goto FirstPersonLegsStand
	}
}