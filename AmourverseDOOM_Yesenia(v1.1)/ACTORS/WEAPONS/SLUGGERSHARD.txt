ACTOR SlugsLoaded : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 3
}

ACTOR Sluggershard : YeseniaArms Replaces SuperShotgun 23000
{
	DamageType "Ice"
	Weapon.SelectionOrder 11
	Weapon.AmmoType "WaterShells"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.BobSpeed 2.0
	Weapon.BobRangeX 0.6
	Weapon.BobRangeY 0.4
	Inventory.Icon "YSHSA0"
	Inventory.PickupMessage "Picked up the Sluggershard!"
	+NOAUTOFIRE
	+NOAUTOAIM
	States
	{
		Spawn:
		YSHS A 1
		Loop
		
		Select:
		TNT1 A 0 A_JumpIf(GetCVar("y_enablelegs") == 0, 2)
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		SHSS ABC 2
		SHSS D 2 A_PlaySound("slugshrd/select", CHAN_WEAPON, 0.5)
		TNT1 A 0 A_Raise(10)
		Wait
		
		Deselect:
		TNT1 A 0 A_RemoveLight("shatterflash")
		SHSS EVWX 2 A_Lower(25)
		Wait
		
		Ready:
		TNT1 A 0 A_JumpIfInventory("IsClimbing", 1, "LedgeGrab")
		SHSS E 1 A_WeaponReady(WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWUSER3)
		Loop
		
		//This section is for the charge modes for the altfire
		LoadTier2:
		SHSS IJKST 2
		SHSS U 1 A_GiveInventory("SlugsLoaded", 1)
		TNT1 A 0 A_PlaySound("slugshrd/cock", CHAN_WEAPON, 0.7)
		SHSS TSKJI 2
		Goto Ready
		
		LoadTier3:
		SHSS IJK 2
		SHSS ST 3
		SHSS U 1 A_GiveInventory("SlugsLoaded", 1)
		TNT1 A 0 A_PlaySound("slugshrd/cockin", CHAN_WEAPON, 0.7)
		SHSS TS 2
		TNT1 A 0 A_PlaySound("slugshrd/cockout", CHAN_WEAPON, 0.7)
		SHSS KJI 2
		Goto Ready
		
		//Next section is for the shooting modes.
		FireTier1:
		TNT1 A 0 A_AttachLight("shatterflash", "PointLight", "99DDFF", 40, 70, 0)
		SHSS F 1 BRIGHT A_PlaySound("slugshrd/shot", CHAN_WEAPON, 1)
		SHSS G 1 BRIGHT A_FireBullets(5, 5, 10, 6, "IceSlugPuff", FBF_USEAMMO)
		TNT1 A 0 A_RemoveLight("shatterflash")
		SHSS HIJ 2
		TNT1 A 0 A_PlaySound("slugshrd/cock", CHAN_BODY, 0.5)
		TNT1 A 0 A_SetInventory("SlugsLoaded", 1)
		SHSS KLMN 2
		//TNT1 A 0 A_FireCustomMissile("IceSlugCasingSpawner",0,0,0,0, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("WaterBuckCasing", 0, 0, random(80,60), CMF_AIMDIRECTION, random(5,15))
		SHSS QRKJI 2
		Goto Ready
		
		FireTier2:
		TNT1 A 0 A_AttachLight("shatterflash", "PointLight", "99DDFF", 50, 90, 0)
		SHSS F 1 BRIGHT A_PlaySound("slugshrd/shot", CHAN_WEAPON, 1.5)
		SHSS G 1 BRIGHT A_FireBullets(8, 8, 20, 6, "IceSlugPuff", 0)
		TNT1 A 1 A_Recoil(2)
		TNT1 A 0 A_JumpIfInventory("PowerInfiniteAmmo", 1, 2)
		TNT1 A 0 A_TakeInventory("WaterShells", 2)
		TNT1 A 0 A_RemoveLight("shatterflash")
		SHSS HIJ 2
		TNT1 A 0 A_PlaySound("slugshrd/cockin", CHAN_BODY, 0.5)
		TNT1 A 0 A_SetInventory("SlugsLoaded", 1)
		SHSS KLM 2
		TNT1 A 0 A_PlaySound("slugshrd/release", CHAN_BODY, 0.7)
		TNT1 A 0 A_CustomMissile("WaterBuckCasing", 0, 0, random(80,60), CMF_AIMDIRECTION, random(5,15))
		TNT1 A 0 A_CustomMissile("WaterBuckCasing", 0, 0, random(80,60), CMF_AIMDIRECTION, random(5,15))
		SHSS NOP 2
		TNT1 A 0 A_PlaySound("slugshrd/cockout", CHAN_BODY, 0.5)
		SHSS QRKJI 2
		Goto Ready
		
		FireTier3:
		TNT1 A 0 A_AttachLight("shatterflash", "PointLight", "99DDFF", 60, 120, 0)
		SHSS F 1 BRIGHT A_PlaySound("slugshrd/shot", CHAN_WEAPON, 2)
		SHSS G 1 BRIGHT A_FireBullets(12, 12, 40, 8, "IceSlugPuff", 0)
		TNT1 A 1 A_Recoil(6)
		TNT1 A 0 A_JumpIfInventory("PowerInfiniteAmmo", 1, 2)
		TNT1 A 0 A_TakeInventory("WaterShells", 3)
		TNT1 A 0 A_RemoveLight("shatterflash")
		SHSS HIJ 2
		TNT1 A 0 A_PlaySound("slugshrd/cockin", CHAN_BODY, 0.5)
		TNT1 A 0 A_SetInventory("SlugsLoaded", 1)
		SHSS KLM 2
		TNT1 A 0 A_PlaySound("slugshrd/release", CHAN_BODY, 0.7)
		TNT1 A 0 A_CustomMissile("WaterBuckCasing", 0, 0, random(80,60), CMF_AIMDIRECTION, random(15,15))
		TNT1 A 0 A_CustomMissile("WaterBuckCasing", 0, 0, random(80,60), CMF_AIMDIRECTION, random(15,15))
		TNT1 A 0 A_CustomMissile("WaterBuckCasing", 0, 0, random(80,60), CMF_AIMDIRECTION, random(15,15))
		SHSS NOPNOPNOP 2
		TNT1 A 0 A_PlaySound("slugshrd/cockout", CHAN_BODY, 0.5)
		SHSS QRKJI 2
		Goto Ready
		
		//Main Firing states
		Fire:
		TNT1 A 0 A_JumpIfInventory("SlugsLoaded", 3, "FireTier3")
		TNT1 A 0 A_JumpIfInventory("SlugsLoaded", 2, "FireTier2")
		TNT1 A 0 A_JumpIfInventory("SlugsLoaded", 1, "FireTier1")
		Wait
		
		AltFire:
		TNT1 A 0 A_JumpIfInventory("SlugsLoaded", 3, "Ready")
		TNT1 A 0 A_JumpIfInventory("SlugsLoaded", 2, "LoadTier3")
		TNT1 A 0 A_JumpIfInventory("SlugsLoaded", 1, "LoadTier2")
		
		//User actions and reselect
		
		Reselect:
		SHSS XWV 2
		Goto Ready
		
		User1:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Kick")
		Goto Ready
		
		User2:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Slide")
		Goto Ready
		
		User3:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "GroundSlam")
		Goto Ready
	}
}