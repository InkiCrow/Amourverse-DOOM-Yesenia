ACTOR BusterBigShot
{
	scale 1
	height 0
	radius 6
	damage 6
	speed 75
	deathsound "BSTR_HIT"
	Decal "BSTPLAS"
	PROJECTILE
	+FORCEPAIN
	
	States
	{
		Spawn:
		TNT1 A 0 A_AttachLight("bustershot", "PointLight", "B3FFD9", 20, 35, 0)
		BTBP A 2 Bright
		Loop
		
		Death:
		TNT1 A 0 A_AttachLight("bustershot", "PointLight", "B3FFD9", 40, 70, 0)
		BSTB BCD 2 Bright
		TNT1 A 0 A_RemoveLight("bustershot")
		Stop
	}
}

ACTOR Buster : YeseniaArms Replaces Pistol 21000
{
	Weapon.SelectionOrder 10
	Weapon.AmmoType "BusterClip"
	Weapon.AmmoUse 1
	Weapon.BobSpeed 2.0
	Weapon.BobRangeX 0.4
	Weapon.BobRangeY 0.2
	Inventory.Icon "PBSTA0"
	+ALT_USES_BOTH
	+NOAUTOFIRE
	+NOAUTOAIM
	States
	{
		Spawn:
		PBST A 1
		Loop
		
		Select:
		TNT1 A 0 A_JumpIf(GetCVar("y_enablelegs") == 0, 2)
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 0 A_PlaySound("buster/spin", CHAN_BODY, 0.6)
		YBST ABCDEFGHI 2 A_Raise(10)
		//YPCH D A_Raise
		Wait
		
		Deselect:
		TNT1 A 0 A_RemoveLight("busterflash")
		YBST OPQ 2 A_Lower(25)
		Wait
		
		Reselect:
		YBST ABCDEFGHI 2
		Goto Ready
		
		Ready:
		TNT1 A 0 A_JumpIfInventory("IsClimbing", 1, "LedgeGrab")
		YBST J 1 A_WeaponReady(WRF_ALLOWUSER1|WRF_ALLOWUSER2|WRF_ALLOWUSER3)
		Loop
		
		Fire:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		TNT1 A 0 A_AttachLight("busterflash", "PointLight", "B3FFD9", 40, 65, 0)
		YBST K 2 
		YBST K 0 A_PlaySound ("buster/shot", CHAN_WEAPON, 0.7)
		TNT1 A 0 A_FireProjectile("BusterShot", 0, TRUE, 0, 5)
		TNT1 A 0 A_RemoveLight("busterflash")
		YBST LMN 2
		Goto Ready
		
		AltFire:
		TNT1 A 0 A_JumpIfInventory("BusterClip", 3, "ConfirmAltFire")
		Goto Ready
		
		ConfirmAltFire:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		YBST K 0 A_PlaySound ("buster/bigshot", CHAN_WEAPON, 0.7)
		YBST J 6
		TNT1 A 0 A_AttachLight("busterflash", "PointLight", "B3FFD9", 50, 80, 0)
		YBST K 2
		TNT1 A 0 A_FireProjectile("BusterBigShot", 0, FALSE, 0, 5)
		TNT1 A 0 A_TakeInventory("BusterClip", 3)
		TNT1 A 0 A_RemoveLight("busterflash")
		YBST LMIH 2
		TNT1 A 0 A_PlaySound ("buster/spin", CHAN_BODY, 0.6)
		YBST CDECDE 2
		YBST F 2 A_PlaySound ("buster/grab", CHAN_BODY, 0.6)
		YBST GHI 2
		Goto Ready
		
		User1:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Kick")
		Goto Ready
		
		User2:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "Slide")
		Goto Ready
		
		User3:
		TNT1 A 0 A_TakeInventory("IsClimbing", 1)
		EMPT A 0 A_Jump(256, "GroundSlam")
		Goto Ready
	}


}